# Copyright 2014 The Chromium Authors. All rights reserved.
# Use of this source code is governed by a BSD-style license that can be
# found in the LICENSE file.

if (is_android) {
  import("//build/config/android/config.gni")
}

# TODO(rohitrao): sessions_core is defined as a source_set because it declares a
# static function that it does not define.  This prevents it from linking as a
# shared_library.  It also cannot be a static_library because it will be linked
# into multiple shared libraries.  Revisit this setup if necessary.
source_set("sessions_core") {
  sources = [
    "core/serialized_navigation_driver.h",
    "serialized_navigation_entry.cc",
    "serialized_navigation_entry.h",
    "session_id.cc",
    "session_id.h",
  ]

  defines = [ "SESSIONS_IMPLEMENTATION" ]

  deps = [
    "//base",
    "//skia",
    "//third_party/protobuf:protobuf_lite",
    "//ui/base",
    "//url",
  ]

  if (!is_android || !is_android_webview_build) {
    deps += [ "//sync" ]
  }
}

static_library("test_support") {
  testonly = true
  sources = [
    "serialized_navigation_entry_test_helper.cc",
    "serialized_navigation_entry_test_helper.h",
  ]

  deps = [
    "//skia",
    "//testing/gtest",
  ]

  if (!is_android || !is_android_webview_build) {
    deps += [ "//sync" ]
  }
}

if (!is_ios) {
  component("sessions_content") {
    sources = [
      "content/content_serialized_navigation_builder.cc",
      "content/content_serialized_navigation_builder.h",
      "content/content_serialized_navigation_driver.cc",
      "content/content_serialized_navigation_driver.h",
    ]

    defines = [ "SESSIONS_IMPLEMENTATION" ]

    deps = [
      ":sessions_core",
      "//base",
      "//base/third_party/dynamic_annotations",
      "//content/public/browser",
      "//ui/base",
      "//url",
    ]
  }
}
